%import common.ESCAPED_STRING
%import common.WS
%import common.SH_COMMENT

%ignore WS
%ignore SH_COMMENT

NUMBER: /0x[0-9a-zA-Z]+/ | /[0-9]+/
ID: /[_a-zA-Z][_a-zA-Z0-9]*/

start: (def_msg | def_struct | include) *

include: "include" ID ":" includes ";"
includes: ID+

def_msg: "msg" ID  "{" type [subtype] request reply "}"

?type: "type" ":" NUMBER ";"
?subtype: "subtype" ":" NUMBER ";"

?request: "request" embedded_struct
?reply: "reply" embedded_struct

?embedded_struct: struct_body -> embedded_struct_body
    | type_ref ";"

type_ref: ID 
    | ID "[" NUMBER "]" -> type_ref_array

def_struct: "struct" ID struct_body

struct_body: "{" field* "}"

field: ID ":" type_ref [presentation] ";"
presentation: "hex" -> p_hex 
    | "oct" -> p_oct
